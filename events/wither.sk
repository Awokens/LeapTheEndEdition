


on death of wither skeleton:
	metadata tag "minion" of victim is set
	cancel drops of items




on damage:
	if attacker is a wither:
		if chance of 5%:
			apply darkness tier 2 to victim for 1 minute replacing existing effect
		apply wither tier 2 to victim for 15 seconds replacing existing effect
		push victim (vector from attacker to victim) at speed 0.8
		exit
	set metadata tag "attacker" of victim to attacker
	victim is a wither
	if chance of 10%:
		push victim (vector from attacker to victim) at speed 5
		show huge explosion at victim
		add 20 to victim's health
		apply invisibility to victim for 5 seconds replacing existing effect
		apply resistance 3 to victim for 5 seconds replacing existing effect
		set {_blocks::*} to blocks in radius 5 of victim
		play sound "minecraft:entity.wither.ambient" at victim
		loop (random integer from 5 and 8) times:
			set {_b} to random element out of {_blocks::*}
			set {_entity} to random element out of (wither skeleton, zombie, husk, skeleton, spider, and cave spider)
			spawn {_entity} at {_b}
			set {_e} to last spawned entity
			set metadata tag "minion" of {_e} to victim
			if chance of 25%:
				apply invisibility to {_e} for 1 minute
			apply fire resistance to {_e} for 1 minute
			apply speed tier 3 to {_e} for 1 minute
			apply strength tier 3 to {_e} for 1 minute
			play sound "minecraft:entity.wither.break_block" at volume 0.2 at {_b}
			play sound "minecraft:entity.wither_skeleton.step" at {_b}
			wait 5 tick
			show cloud at {_b}

on spawn of wither:
	set event-entity's max health to 100
	set event-entity's health to 100
	while event-entity is alive:
		if y-coord of event-entity < -64:
			apply levitation 2 to event-entity for 5 second replacing existing effect
		wait 1 second

on death of wither:

	cancel drops
	set {_amount} to random integer from 1 and 1 + looting level of attacker's held item
	drop {_amount} of netherite scrap at victim

	loop entities:
		loop-entity is a mob
		if metadata tag "minion" of loop-entity is victim:
			kill loop-entity
			play sound "minecraft:entity.wither.break_block" at volume 0.2 at loop-entity
			play sound "minecraft:entity.wither_skeleton.step" at loop-entity
			wait 5 tick



on target:

	if bound id at event-entity contains "spawn":
		cancel event

	if event-entity is a mob:
		if target is a mob:
			cancel event